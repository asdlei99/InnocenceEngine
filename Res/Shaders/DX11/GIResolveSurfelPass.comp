// shadertype=hlsl
#include "common/common.hlsl"

struct ComputeInputType
{
	uint3 groupID : SV_GroupID;           // 3D index of the thread group in the dispatch.
	uint3 groupThreadID : SV_GroupThreadID;     // 3D index of local thread ID in a thread group.
	uint3 dispatchThreadID : SV_DispatchThreadID;  // 3D index of global thread ID in the dispatch.
	uint  groupIndex : SV_GroupIndex;        // Flattened local index of the thread within a thread group.
};

RWStructuredBuffer<Surfel> in_surfels : register(u0);
RWStructuredBuffer<float4> out_irradiance : register(u1);

[numthreads(8, 8, 8)]
void main(ComputeInputType input)
{
	if (input.dispatchThreadID.x < dispatchParams[2].numThreads.x
		&& input.dispatchThreadID.y < dispatchParams[2].numThreads.y
		&& input.dispatchThreadID.z < dispatchParams[2].numThreads.z)
	{
		uint l_surfelIndex = input.dispatchThreadID.x + (input.dispatchThreadID.y * dispatchParams[2].numThreads.x) + (input.dispatchThreadID.z * dispatchParams[2].numThreads.x * dispatchParams[2].numThreads.y);
		Surfel l_surfel = in_surfels[l_surfelIndex];

		float3 posWS = l_surfel.pos.xyz;
		float metallic = l_surfel.MRAT.x;
		float3 normalWS = l_surfel.normal.xyz;
		float3 albedo = l_surfel.albedo.xyz;

		float3 N = normalize(normalWS);
		float3 V = normalize(cameraCBuffer.globalPos.xyz - posWS);
		float3 L = normalize(-dirLight_dir.xyz);
		float NdotV = max(dot(N, V), 0.0);
		float NdotL = max(dot(N, L), 0.0);

		float kD = 1.0 - metallic;

		// Diffuse BRDF
		float3 BRDF = kD * albedo / PI;

		float3 Lo = BRDF * dirLight_luminance.xyz * NdotL;

		out_irradiance[l_surfelIndex] = float4(Lo, 1.0);
	}
}